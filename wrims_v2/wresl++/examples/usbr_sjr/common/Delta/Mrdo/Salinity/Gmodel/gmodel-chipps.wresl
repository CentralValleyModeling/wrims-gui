! Armin Munevar 11/20/99

! G-MODEL BELOW IS FOR CHIPPS (CHS)
! ****************************************************************************
! ****************************************************************************
! Coefficients for Chipps  
define g_So_chs     {value 60.0     }        ! millimhos/cm
define g_Sb_chs     {value 0.180    }        ! millimhos/cm
define g_alpha_chs  {value 0.004613 }        ! 1/cfs
define g_beta_chs   {value 475.0    }        ! ft^3
define g_n_chs      {value 0.7      }        ! no units

! define Dvars needed for this station's G model

define Go_chs {std kind 'GMODEL-PARAM' units 'CFS'}

! **************************************************************************************
! Calculate Go for the current period. This is the beginning of period G value based upon
! last month's flow and beginning of last month's G value, Go(-1). Units are cfs

define Go_hist_chs {value	NDO(-1)/(1 + (NDO(-1)/Go_chs(-1) - 1)
                          *EXP(-NDO(-1)*g_delT/g_beta_chs))           }

goal setGo_chs {Go_chs = Go_hist_chs}


! *************************************************************************************
! Get salinity standard for this particular station.
! Standard may specify two salinity requirements for different days of the month. In 
! this case, 2 standards are read from the table along with their corresponding dates.

! first standard
define EC_std_chs1 {
       case Oct_thru_May {
         condition         month >=OCT .and. month <=MAY
         select            ECstandard1
         from              salinity_std_chs 
         where             month=month }
       case no_standard {                       ! dummy value when standard not in effect
         condition         always
         value             g_So_chs }  }        ! this makes the req'mt = 0

! get the dates for this standard
define begday_chs1 {
      case Oct_thru_May {
        condition       month >=OCT .and. month <=MAY
        select          beginday1
        from            salinity_std_chs
        where           month=month }
       case no_standard {
         condition        always
         value            0 }
}
define endday_chs1 {
      case Oct_thru_May {
        condition       month >=OCT .and. month <=MAY
        select          endday1
        from            salinity_std_chs
        where           month=month }
       case no_standard {
         condition        always
         value            0 }
}

! second standard
define EC_std_chs2 {
       case Oct_thru_May {
         condition         month >=OCT .and. month <=MAY
         select            ECstandard2
         from              salinity_std_chs 
         where             month=month }
       case no_standard {                       ! dummy value when standard not in effect
         condition         always
         value             g_So_chs }  }        ! this makes the req'mt = 0

! get the dates for this standard
define begday_chs2 {
      case Oct_thru_May {
        condition       month >=OCT .and. month <=MAY
        select          beginday2
        from            salinity_std_chs
        where           month=month }
       case no_standard {
         condition        always
         value            0 }
}
define endday_chs2 {
      case Oct_thru_May {
        condition       month >=OCT .and. month <=MAY
        select          endday2
        from            salinity_std_chs
        where           month=month }
       case no_standard {
         condition        always
         value            0 }
}

! *************************************************************************************
! Get required Delta Outflow to meet this salinity standard

define DO_req_chs1 {
     case standardON {
          condition      month >=OCT .and. month <=MAY 
          value          min(flowcap_salt, GET_REQ_DO(Go_hist_chs,EC_std_chs1,g_So_chs,
          											  g_Sb_chs,g_alpha_chs,g_beta_chs,g_n_chs) ) }
     case standardOFF {
          condition      always
          value          0.0      }    }

define DO_req_chs2 {
     case standardON {
          condition      month >=OCT .and. month <=MAY 
          value          min(flowcap_salt, GET_REQ_DO(Go_hist_chs,EC_std_chs2,g_So_chs,
          											  g_Sb_chs,g_alpha_chs,g_beta_chs,g_n_chs) ) }
     case standardOFF {
          condition      always
          value          0.0      }    }
  

! *****************************************************************************************
! Get Salinity at this location at the end of last period (beginning of this period).  
! This is for output purposes only.

define salinity_prv_chs {std kind 'SALINITY-PREV-COL' units 'MMHOS/CM'}

define Gavg_prv_chs {value	NDO(-1) + (g_beta_chs/g_delT)*log((1+(NDO(-1)/Go_chs(-1) - 1)*
                         		EXP(-NDO(-1)*g_delT/g_beta_chs))/(NDO(-1)/Go_chs(-1)))   }

goal salinity_prv_chs {salinity_prv_chs = (g_So_chs - g_Sb_chs)*EXP(-g_alpha_chs*POW(Gavg_prv_chs,g_n_chs)) 
                                    + g_Sb_chs}
