! stan_FW_min.wresl
! Edward Chang
! 12/02/1999
! Joint Model
! This code calculates the minimum fish and wildlife flows on the Stanislaus river
! Revised by R. Field, 01/15/2004, Reclamation, removed stanmin_add from cycle 1
! it exists in cycle 6 InstreamFromOID.wresl
! Revised by C. Hsu, 3/10/03, USBR, for review and documentation project
!#####################################################################################
/*
The minimum fish and wildlife flows are determined using the New Melones forecast, NMforeast1.
This forecast determines an annual delivery requirement from a look-up table.  From the annual
delivery requirement another look-up table is used to determine the monthly requirements.
In April and May, a pulse flow requirement is also calculated, but the pulse flow only applies
during the last 16 days in April and the first 15 days in May.  The goal of the resulting monthly
requirement is to maintain a minimum fish and wildlife flow downstream of Goodwin Dam.  To ensure
a minimum flow, it is only necessary to impose this requirement on reaches C_STS059 and C_STS017
*/


/*
Fish & Wildlife flows
*/
! OLD -- W/O BO
! <NoBO>   define stanfish_yr   {select stanf from stan_yr given NMF=NMforecast1 use linear }
! <NoBO>   define stanfish_yrDV {alias  stanfish_yr units 'taf'}

! NEW -- W/  BO
! In April uses pre-RPA std (in order to set VAMP baseline), rest of year uses RPA std
define stanfish_yr {
    case April {
        condition month==APR
        select stanf from stan_yr_90 given NMF = NMforecast1 use linear }
    case notApr {
        condition always 
        select stanf from stan_yr    given NMF = NMforecast1 use linear }
}
! Alias stanfish_yr for output
define stanfish_yrDV {alias stanfish_yr units 'taf'}


! OLD -- W/O BO
! <NoBO>   define stanmin {
! <NoBO>       select          stanfish from stan_monfish given  stanyr = stanfish_yr use linear
! <NoBO>       where           month = month }

! NEW -- W/  BO
! Define minimum flows for Cycle 1 to be imposed on releases from New Melones
define stanmin {
    case April {
        condition month==APR
        select stanfish from stan_monfish given stanyr=stanfish_yr use linear where month=month}
    case notApr {
        condition always
        select flow     from stan_rpa     given stanyr=stanfish_yr use linear where month=month}
}
define stanmin_dv {alias stanmin kind 'fish-flow-req' units 'cfs'}


! Define revised total annual fish releases
! Base MIF + Enhanced flow based on estimated delivery, estimated inflow, and desired target storage
define stanfish_yr2 { select stanf from stan_yr given NMF=NMforecast1 use linear }
/* In NoBO, but commented (also commented in BO)
define stanfish_yr2 {
    case firstyr {
        condition wateryear == 1922 .and. month <= mar
        value 98.9 + stan_dem_est + min(204.,max(NMforecast3-((310.-stan_dem_est)-stan_dem_est)-1700.,0.0))
    }
    case march {
        condition month == mar
                value 98.9 + stan_dem_est + min(204.,max(NMforecast3-((310.-stan_dem_est)-stan_dem_est)-1700.,0.0))
        }
        case otherwise {
                condition always
                value 98.9 + stan_dem_estdv(prevMar) + min(204.,max(NMforecast3dv(prevMar)-((310.-stan_dem_estdv(prevMar))-stan_dem_estdv(prevMar))-1700.,0.0))
        }
} */
define stanfish_yr2dv {alias  stanfish_yr2 units 'taf'}
define stanmin2       {select stanfish from stan_monfish given stanyr=stanfish_yr2 use linear where month = month}
define stanmin2dv     {alias  stanmin2 units 'cfs'}


! In BO, but already commented...
! Stanislaus River Temperature Management flow surrogates for June - Sep
! define stantemp {
!        case June_Sep {
!                 condition month >= jun .and. month <= sep
!                 select tflow from stan_temperature given stanyr = stanfish_yr2 use minimum where month = month}
!        case otherwise {
!             condition always
!             value 0.0}
! }


! Set limits on minimum fish flows for Goodwin, CALSIM uses internal units of cfs
! <Commented in BO> goal setC_STS059cycle1 { C_STS059_mif < max(stanmin2,stantemp) }
goal setC_STS059cycle1 { C_STS059_mif < stanmin }


! Set limits on minimum fish flows for Ripon, CALSIM uses internal units of cfs
! <Commented in BO> goal setC_STS017cycle1 { C_STS017_mif < max(stanmin2,stantemp) }
goal setC_STS017cycle1 { C_STS017_mif < stanmin }


! Set instream flow requirements 
! <From BO>    define C520InstreamTgt {value max(0., OIDInstreamToBeMet*TAF_cfs - stanmin)}
define C_STS059_InstreamTgt   {value max(0.0, OIDInstreamToBeMet*TAF_cfs - stanmin)}
! <From BO>    define C520INSTGTDV {alias C520INSTREAMTGT kind 'output-cfs' units 'cfs'}
define C_STS059_InstreamTgtDV {alias OIDInstreamToBeMet         kind 'alias' units 'taf'}
! <From BO>    goal   setC520INSTREAM1 {C520INSTREAM = C520INSTREAMTGT}
goal   setC_STS059Instream1   {C_STS059Instream = C_STS059_InstreamTgt}


