! Friant_operation.wresl
! MBK (consultants for USBR)
! 09/15/2002
! Determination of Friant San Joaquin River minimum release
! Revised by C. Hsu, 02/25/03, USBR for review and documentation project
!##############################################################################
/*
This file defines the Madera and Friant-Kern Canal demands and deliveries
*/
! Define Fraint-Kern Canal Demands
define dem_D_MLRTN_FRK {std kind 'Friant-Demand' units 'TAF' }
! Define Madera Canal demands
define dem_D_MLRTN_MDC {std kind 'Friant-Demand' units 'TAF' }
! Friant-Kern Canal release
goal set_FK_release { dem_D_MLRTN_FRK = D_MLRTN_FRK_ini - FK_Adj + FK_add }
! Madera Canal release
goal set_Madera_release { dem_D_MLRTN_MDC = D_MLRTN_MDC_ini + Madera_add }

/*
This file defines the total deliveries from Friant which includes Madera Canal and Friant-Kern Class I & II, canal losses,
and additional delivery
*/
! Total delivery from Friant
define total_Fr_del {
    value    Madera_C1_del + Madera_C2_del + Madera_loss + Madera_add
             + FK_C1_del + FK_C2_del + FK_loss - FK_Adj + FK_add }

! Friant Kern canal Class 2 water including adjustment
define FK_final_C2 {value FK_C2_del - FK_Adj}
! Alias value for Madera Class 1 water delivery
define madera_C1_out {alias madera_C1_del kind 'flow_delivery' units 'taf'}
! Alias value for Madera Class 2 water delivery
define madera_C2_out {alias madera_C2_del kind 'flow_delivery' units 'taf'}
! Alias value for Madera loss
define madera_loss_out {alias madera_loss kind 'flow_delivery' units 'taf'}
! Alias value for additional Madera Canal delivery
define madera_add_out {alias madera_add kind 'flow_delivery' units 'taf'}
! Alias value for Friant Kern Class 1 water delivery
define FK_C1_del_out {alias FK_C1_del kind 'flow_delivery' units 'taf'}
! Alias value for Friant Kern Class 2 water delivery
define FK_C2_del_out {alias FK_C2_del kind 'flow_delivery' units 'taf'}
! Alias value for Friant Kern loss
define FK_loss_out {alias FK_loss kind 'flow_delivery' units 'taf'}
! Alias value for Friant Kern additianal delivery
define FK_add_out {alias FK_add kind 'flow_delivery' units 'taf'}
! Alias value for Friant Kern cnal Class 2 water including adjustment
define FK_final_C2_out {alias FK_final_C2 kind 'flow_delivery' units 'taf'}

! Define total canal adjustments to river release
define Fr_tot_adj { value FK_adj - FK_add - Madera_add }
! Snowmelt release (SJR) determination, total snowmelt release minus 215 delivery of snowmelt release
define Fr_snow_rel { value max(0.0,(fr_for_spill * Fr_srp / 100.) - FK_add - Madera_add) }

/*
Snowmelt release
C_MLRTNa target is computed considering releases that will be made for minimum
flows and deliveries.  So C_MLRTNa is above and beyond C_MLRTNm.
*/

define est_RF_to_MC {value max(0.0, S_MLRTN(-1) + I_MLRTN *cfs_taf - curr_Fr_del - Friant_est_evap - s_MLRTNlevel4 - Madera_add - FK_add)}

define max_SM_to_MC {value max(0.,min(Fr_snow_rel, Madera_cap_head * cfs_taf - D_MLRTN_MDC_ini - Madera_add))}

!Limit Madera Surplus release to estimated snowmelt release and flood release
goal lim_D_MLRTNF_surplus {D_MLRTN_MDC_F < est_RF_to_MC*taf_cfs + max_SM_to_MC*taf_cfs}
!Limit C18A to snowmelt release

goal set_C_MLRTNA { C_MLRTNa < Fr_snow_rel * taf_cfs}

!During flood or snowmelt spills, approximately 7% of the spill will be to Madera Canal capped at 1000 cfs with half
!going to the Fresno River and half going to Chowchilla

goal set_D_MLRTN_MDCf_max {D_MLRTN_MDC_F < max(0., Madera_cap_head - D_MLRTN_MDC_ini * taf_cfs - Madera_add * taf_cfs) }
goal lim_D_MLRTN_MDCf_by_C18AandF {D_MLRTN_MDC_F < 0.07*C_MLRTNA + 0.07*C_MLRTNF}
goal lim_D_MLRTN_MDCf_1000 {D_MLRTN_MDC_F < 1000.}
goal set_D_MLRTN_MDCf {D_MLRTN_MDC_F = SP_MDC019_FRS045 + C_MDC032F}
goal share_D_MLRTN_MDCf {
    lhs C_MDC032F
    rhs SP_MDC019_FRS045
    lhs<rhs penalty 5
    lhs>rhs penalty 5 }

! Set limits on Friant-Kern Canal releases
goal set_FK_rel { D_MLRTN_FRK000 < dem_D_MLRTN_FRK * taf_cfs }
! Set limits on Madera Canal releases
goal set_M_rel  { D_MLRTN_MDC006 < dem_D_MLRTN_MDC * taf_cfs + D_MLRTN_MDC_F}
goal set_max_MC { D_MLRTN_MDC006 < Madera_cap_head}
! Madera Canal breakdown
goal set_D_MLRTN_MDC_main { D_MLRTN_MDC_main < max(0., D_MLRTN_MDC_ini * taf_cfs - Madera_loss*taf_cfs)}
! Set limits on Madera Canal 215 water
goal set_D_MLRTN_MDC_215 { D_MLRTN_MDC_215 < Madera_add * taf_cfs }

define dem_D_MLRTN_MDC_A  {alias dem_D_MLRTN_MDC*taf_cfs kind 'debug' units 'cfs'}
define dem_D_MLRTN_FRK_A  {alias dem_D_MLRTN_FRK*taf_cfs kind 'debug' units 'cfs'}


! IMF -- additional aliases for debugging
define Madera_tot_del_out  {alias Madera_tot_del    kind 'debug' units 'none'}
define Madera_cap_head_out {alias Madera_cap_head   kind 'debug' units 'none'}
define Madera_tot_pct_out  {alias Madera_tot_pct    kind 'debug' units 'none'}
define Ann_Madera_C1_out   {alias Ann_Madera_C1     kind 'debug' units 'none'}
define Ann_Madera_C2_out   {alias Ann_Madera_C2     kind 'debug' units 'none'}
define Ann_Madera_tot_out  {alias Ann_Madera_tot    kind 'debug' units 'none'}
define class1_alloc_out    {alias class1_alloc      kind 'debug' units 'none'}
define class2_alloc_out    {alias class2_alloc      kind 'debug' units 'none'}
define pct_c2_madera_out   {alias pct_c2_madera     kind 'debug' units 'none'}
define class2_max_out      {alias class2_max        kind 'debug' units 'none'}
define Fr_WS_For_out       {alias Fr_WS_For         kind 'debug' units 'none'}
define D_MLRTN_MDC_ini_out {alias D_MLRTN_MDC_ini   kind 'debug' units 'none'}
define fr_for_spill_out    {alias fr_for_spill      kind 'debug' units 'none'}
define fr_rain_rel_out     {alias fr_rain_rel       kind 'debug' units 'none'}
define fr_snow_rel_vol_out {alias fr_snow_rel_vol   kind 'debug' units 'none'}
define snow_rel_for_out    {alias snow_rel_for      kind 'debug' units 'none'}
define I_MLRTN_thrusep_out {alias I_MLRTN_thrusep   kind 'debug' units 'none'}
define FREvap_sep_out      {alias FREvap_sep        kind 'debug' units 'none'}
define C_MLRTN_min_sep_out {alias C_MLRTN_min_sep   kind 'debug' units 'none'}
define FRloss_sep_out      {alias FRloss_sep        kind 'debug' units 'none'}
define I_MLRTN_thrujun_out {alias I_MLRTN_thrujun   kind 'debug' units 'none'}
define FR_del_est_jun_out  {alias FR_del_est_jun    kind 'debug' units 'none'}
define C_MLRTN_min_jun_out {alias C_MLRTN_min_jun   kind 'debug' units 'none'}
define FR_losses_jun_out   {alias FR_losses_jun     kind 'debug' units 'none'}

