! A. Munevar 11/20/99

! Defines Standard allowable & Max allowable pumping at Tracy.  Tracy can increase pumping
! up to 1/3 of San Joaquin River flow at Vernalis if flow at Vernalis is greater than 1000 cfs.

!   If SJR flow @ Vernalis > 1000 cfs then,
!   Tracy_max_allow = Tracy_standard_allow + 1/3*(SJR @ Vernalis)
!   But only upto physical capacity.

! Number of days in which the Tracy limit *IS* increased by 1/3 of SJR flow
define TracyDaysIncr {
       select   DaysIncr
       from     TracyLimits
       where    month=month
}

! Number of days in which the Tracy limit *IS NOT* increased by 1/3 of SJR flow
define TracyDaysNoIncr { value daysin - TracyDaysIncr}

! Finds the Max Pump Capacity with the allowable increase
define TracyMax1 {
/*      case Increase {
        condition   C_SJR070[-1] > 1000.0   ! Change back to cycle stuff
        value   min(TracyCapPermit + C_SJR070[-1]/3.0, TracyCapPhys) }   ! Change back to cycle stuff
*/
       case NoIncrease {
            condition always
            value     TracyCapPermit }
}

! Limit Pumping to allowable, weight for days in which increase is allowed
define TracyMaxAllow {value TracyMax1*TracyDaysIncr/daysin + TracyCapPermit*TracyDaysNoIncr/daysin }

! Limit Pumping to Allowable and Physical Capacity, whichever is less
goal tracyLessThanAllow { C_DMC003 < TracyMaxAllow}
goal tracyLessThanPhys  { C_DMC003 < TracyCapPhys}

! Output Allowable Pumping Cap for Checking Only
define TracyAllowOut {alias TracyMaxAllow kind 'FLOW-ALLOW-TRACY' UNITS 'CFS' }

! Tracy PP Minimum Cap
define tracyminpump {
      case tracylowshasta {
        condition S_SHSTA(-1)+S_SHSTAmod /*+ S44(-1)*/ < 1500.
        value 600.}
      case tracyhighshasta   {
        condition always
        value 800.}}
        
goal settracyminpump {
    lhs DMC003_cvp
    rhs tracyminpump
        
        lhs>rhs penalty 0
        lhs<rhs penalty 2700 }


