/************************************
feather_special_FAM.wresl

Hao Xie and Nazrul Islam DWR 2/6/2012

Special Feather River Flow Requirments for FAM

**************************************/

!=========================================================================
! Special Feather River Fish Criteria for Oct 15 - Nov 30 @ C_Therm (C203) 
!=========================================================================

define oct_trigger_cfs {                   !4000 cfs 
        select    target
        from      feather_fish_203   
        where   month = OCT}

define nov_trigger_cfs {                    !2500 cfs
        select    target
        from      feather_fish_203
        where   month = NOV}

define(FAMmonths) minflow_C_Therm {        
  case October {                            !minflow_C_Therm + 500 = 500 cfs
    condition mv($m) == OCT
    value   0
  }
  case November1 {                     
    condition   mv($m) == NOV .AND.
        int(C_Therm[FAM_nonlinear](prevOCT)) <= oct_trigger_cfs
    value   0
  }
  case November2 {              
    condition mv($m) == NOV .AND.
        S_Orovl[FAM_nonlinear](prevOCT)+1. >= s_OrovlLevel5[FAM_nonlinear](prevOCT)
    value   0
  }
  case November3 {                         !minflow_C_Therm + 500 = C_Therm(prevOCT) cfs
    condition mv($m) == NOV
    value   C_Therm[FAM_nonlinear](prevOCT) - 500
  }

  case DecemberToMarch1 {                  !minflow_C_Therm + 500 = max(C_Therm(prevOCT),C_Therm(prevNOV))
    condition mv($m) >= DEC .AND. mv($m) <= MAR .AND.
        int(C_Therm[FAM_nonlinear](prevOCT)) > oct_trigger_cfs .AND.
        S_Orovl[FAM_nonlinear](prevOCT)+1. < s_OrovlLevel5[FAM_nonlinear](prevOCT) .AND.
        int(C_Therm[FAM_nonlinear](prevNOV)) > nov_trigger_cfs .AND.
        S_Orovl[FAM_nonlinear](prevNOV)+1. < s_OrovlLevel5[FAM_nonlinear](prevNOV)
    value   max(C_Therm[FAM_nonlinear](prevOCT) - 500, C_Therm[FAM_nonlinear](prevNOV) - 500)
  }
  case DecemberToMarch2 {                  !minflow_C_Therm + 500 = C_Therm(prevOCT) cfs
    condition mv($m) >= DEC .AND. mv($m) <= MAR .AND.
        int(C_Therm[FAM_nonlinear](prevOCT)) > oct_trigger_cfs .AND.
        S_Orovl[FAM_nonlinear](prevOCT)+1. < s_OrovlLevel5[FAM_nonlinear](prevOCT)
    value   C_Therm[FAM_nonlinear](prevOCT) - 500
  }
  case DecemberToMarch3 {                  !minflow_C_Therm + 500 = C_Therm(prevNOV) cfs
    condition mv($m) >= DEC .AND. mv($m) <= MAR .AND.
        int(C_Therm[FAM_nonlinear](prevNOV)) > nov_trigger_cfs .AND.
        S_Orovl[FAM_nonlinear](prevNOV)+1. < s_OrovlLevel5[FAM_nonlinear](prevNOV)
    value   C_Therm[FAM_nonlinear](prevNOV) - 500
  }
  case otherwise {                         !minflow_C_Therm + 500 = 500 cfs
    condition always
    value   0
  }
}

/* 
 If trigger is exceeded because of flood spills then no minflow is applied.
  If releases for flood control are made then no minimum flow is applied.
  In October and November, try to keep flow at C_Therm < trigger 
  otherwise pay the price of a minflow(=OCT flow or NOV flow - 500cfs) through March.  
  Do this by reducing releases for exports if necessary.
*/

goal(FAMmonths) avoidTrigger {
  lhs C_Therm
  case October {
    condition mv($m) == OCT
    rhs   oct_trigger_cfs
    lhs>rhs   penalty 4600
    lhs<rhs   penalty 0
  }
  case November {
    condition mv($m) == NOV
    rhs   max(nov_trigger_cfs, minflow_C_Therm($m) + 500)
    lhs>rhs   penalty 4600
    lhs<rhs   penalty 0
  }
}

!=========================================================================
! DFG and DWR 1983 agreement to maintain ~1700 cfs minflow from the thermalito
! complex to the Feather River mouth (Just above Verona).
! minflow_C_YubFea is equal to the timeseries "minflow_C223" in CalSim
! The actual minflow for C_Therm, just below the Thermalito complex, is 
! the max of the minflow_C_Therm and minflow_C_YubFea.
!=========================================================================
 
define minflow_C_YubFea {timeseries kind 'FLOW-MIN-REQUIRED' units 'CFS'}
goal(FAMmonths) meetC_Therm_Minflow {C_Therm_MIF($m) < max(minflow_C_Therm($m), minflow_c_YubFea($m))}

!=========================================================================
! DWR maintains flow at the "Mouth of Feather River" based on requirements of diversion 
! pumping facilities. Their flow target depends on upstream Yuba and Oroville inflow, 
! the DWR/DFG agreed flow target above Verona, and a 2800 cfs flow rate related to 
! pumping facility requirements. The timeseris minflowFeaMouth are preprocessed 
! for CalLite Simulations, see details at end of this file. 
!=========================================================================

define minflowFeaMouth {timeseries kind 'FLOW-MIN-REQUIRED' units 'CFS'} !Mouth of Feather River  (232)
goal(FAMmonths) MouthMinFlow {C_YubFea_MIF($m) < minflowFeaMouth($m)}

