/*************************************
Jones_pump_allow_FAM.wresl
 
Hao Xie and Nazrul Islam DWR (02/08/2012)

There is no regulation for Jones at this time for FAM. However, we just mimic Banks PP. 
Defines Standard allowable & Max allowable pumping at Jones.  Jones can increase pumping
up to 1/3 of San Joaquin River flow at Vernalis if flow at Vernalis is greater than 1000 cfs.
    If SJR flow @ Vernalis > 1000 cfs then,
    Jones_max_allow = Jones_standard_allow + 1/3*(SJR @ Vernalis)
    But only upto physical capacity.
**************************************/



! Number of days in which the Jones limit *IS* increased by 1/3 of SJR flow
define(FAMmonths) JonesDaysIncr {
       select   DaysIncr
       from     JonesLimits
       where    month=mv($m)
}

! Number of days in which the Jones limit *IS NOT* increased by 1/3 of SJR flow
define(FAMmonths) JonesDaysNoIncr { value di($m) - JonesDaysIncr($m)}

! Finds the Max Pump Capacity with the allowable increase
define(FAMmonths) JonesMax1 {
       case NoIncrease {
            condition always
            value     JonesCapPermit($m) }
}

! Limit Pumping to allowable, weight for days in which increase is allowed
define(FAMmonths) JonesMaxAllow {value JonesMax1($m)*JonesDaysIncr($m)/di($m) + JonesCapPermit($m)*JonesDaysNoIncr($m)/di($m) }

! Limit Pumping to Allowable and Physical Capacity, whichever is less
goal(FAMmonths) JonesLessThanAllow { D_Jones($m) < JonesMaxAllow($m)}
goal(FAMmonths) JonesLessThanPhys  { D_Jones($m) < JonesCapPhys($m)}

! Output Allowable Pumping Cap for Checking Only
define(FAMmonths) JonesAllowOut {alias JonesMaxAllow($m) kind 'EXPORT-CTRL-JONES' UNITS 'CFS' }

! Jones PP Minimum Cap
define(FAMmonths) Jonesminpump {
    case Joneslowshasta {
        condition S_Shsta[fam_nonlinear]($m-1) < 1500.
        value 600. }
    case Joneslowshastafolsom {
        condition S_Folsm[fam_nonlinear]($m-1) < 400. .and. S_Shsta[fam_nonlinear]($m-1) < 2000. !Better reflection of actual operations by trying to keep
        value 800. }                                           !water from being released for SL storage during low storage
    case Joneshighshasta   {                                   !conditions in the NOD.
        condition always
        value 800. }
}
define(FAMmonths) JonesMinPumpDV {alias JonesMinPump(FAMmonths) kind 'EXPORT-CTRL-JONES' units 'cfs'}

goal(FAMmonths) setJonesminpump {
    lhs D_Jones_cvp($m)
    case Joneslowshasta {
        condition always
    	rhs Jonesminpump($m)
        lhs>rhs penalty (65*int_Shsta2000($m)+(1-int_Shsta2000($m))*65*int_Shsta1500($m)*int_Folsm400($m))*taf_cfs($m) + 1
        lhs<rhs penalty 2700 }
}
